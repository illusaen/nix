{
  flake.modules.nixos.autostart = {
    lib,
    config,
    ...
  }: let
    mkService = entry: {
      wantedBy = ["graphical-session.target"];
      partOf = ["graphical-session.target"];
      after = [
        "graphical-session.target"
      ];
      description = "Autostarts ${entry.name} on login";
      serviceConfig = {
        ExecStart = entry.exec;
        Restart = "on-failure";
      };
    };
    autostartEntryType = lib.types.submodule ({config, ...}: {
      options = {
        package = lib.mkOption {type = lib.types.package;};
        name = lib.mkOption {
          type = lib.types.str;
          default = lib.getName config.package;
        };
        exec = lib.mkOption {
          type = lib.types.str;
          default = "${lib.getExe config.package}";
        };
      };
    });
  in {
    options.systemdAutostart = lib.mkOption {
      type = lib.types.listOf autostartEntryType;
      default = [];
    };
    config.systemd.user.services = config.systemdAutostart |> map (entry: lib.nameValuePair entry.name (mkService entry)) |> builtins.listToAttrs;
  };
}
