WORDCHARS=${WORDCHARS:s#/#}

bindkey '^[b' backward-word
bindkey '^[f' forward-word
bindkey '^[[1;5D' backward-word
bindkey '^[[1;5C' forward-word
bindkey '^W' backward-kill-word

zmodload zsh/terminfo
[[ -n "${terminfo[khome]}" ]] && bindkey "${terminfo[khome]}" beginning-of-line
[[ -n "${terminfo[kend]}" ]] && bindkey "${terminfo[kend]}" end-of-line
[[ -n "${terminfo[kdch1]}" ]] && bindkey "${terminfo[kdch1]}" delete-char
bindkey '^[[H' beginning-of-line
bindkey '^[[F' end-of-line
bindkey '^[[1~' beginning-of-line
bindkey '^[[4~' end-of-line
bindkey '^[[7~' beginning-of-line
bindkey '^[[8~' end-of-line
bindkey '^[[3~' delete-char

dot_cd_accept_line() {
  emulate -L zsh

  if [[ "$BUFFER" =~ '^[[:space:]]*(\.{2,})[[:space:]]*$' ]]; then
    local cmd="$match[1]"
    local levels=$((${#cmd} - 1))
    local target="$PWD"

    while ((levels > 0)) && [[ "$target" != "/" ]]; do
      target="${target:h}"
      ((levels--))
    done

    BUFFER="cd -- ${(q)target}"
  fi

  zle .accept-line
}
zle -N accept-line dot_cd_accept_line

git_commit_with_message() {
  git commit -m "$1"
}

nix_store_for_command() {
  if [ "$#" -ne 1 ]; then
    echo "Usage: nix_store_for_command <command>" >&2
    return 2
  fi

  local command_path resolved_path store_name store_dir
  if ! command_path="$(command -v -- "$1" 2>/dev/null)"; then
    echo "Command not found: $1" >&2
    return 1
  fi

  resolved_path="$(readlink -f -- "$command_path")" || return

  if [[ "$resolved_path" != /nix/store/* ]]; then
    echo "$1 resolves outside /nix/store: $resolved_path" >&2
    return 1
  fi

  store_name="${resolved_path#/nix/store/}"
  store_name="${store_name%%/*}"
  store_dir="/nix/store/$store_name"

  command eza --icons=auto --git "$store_dir"
  echo "Store dir: $store_dir"
}

open_editor() {
  for editor in zeditor nvim vim; do
    if command -v "$editor" >/dev/null 2>&1; then
      "$editor" .
      return
    fi
  done

  echo "Error: no supported editor found (zed, nvim, or vim)." >&2
  return 1
}

autoload -Uz add-zsh-hook
cd_ls_hook() {
  emulate -L zsh
  local file_count=(*(ND))
  if (($#file_count <= 50)); then
    eza --icons=always --group-directories-first
  else
    echo "Large directory ($#file_count files). Skipped auto-ls."
  fi
}
add-zsh-hook chpwd cd_ls_hook
